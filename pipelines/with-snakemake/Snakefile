REFERENCE_URL='http://ftp.ncbi.nlm.nih.gov/genomes/all/GCA_000988525.2_ASM98852v2/GCA_000988525.2_ASM98852v2_genomic.fna.gz'
READS='2492428'
TEMP='./tmp'

THREADS=2

rule all:
    input: 'reads.vcf'

# Download

rule get_reads:
    output: 'reads.sra'
    benchmark: './benchmarks/get_reads.txt'
    shell: '''
        bionode-ncbi download sra {READS} > {TEMP};
        cp {READS}/*.sra {output};
    '''

rule get_reference:
    output: 'reference.genomic.fna.gz'
    benchmark: './benchmarks/get_reference.txt'
    shell: 'curl -s {REFERENCE_URL} -o {output}'

# Extracting

rule extract_reads:
    input: rules.get_reads.output
    output: 'fastq-dump.log'
    benchmark: './benchmarks/extract_reads.txt'
    shell: 'fastq-dump --split-files --skip-technical --gzip {input} > {output}'

# Mapping

rule index_reference:
    input: rules.get_reference.output
    output: 'bwa-index.log'
    benchmark: './benchmarks/index_reference.txt'
    shell: 'bwa index {input} > {output}'

rule sam:
    input:
        reference=rules.get_reference.output,
        reads=rules.extract_reads.output,
        index_files=rules.index_reference.output
    output: 'reads.sam'
    benchmark: './benchmarks/sam.txt'
    shell: 'bwa mem -t {THREADS} {input.reference} reads_1.fastq.gz reads_2.fastq.gz > {output}'

rule reads_unsorted:
    input: rules.sam.output
    output: 'reads.unsorted.bam'
    benchmark: './benchmarks/reads_unsorted.txt'
    shell: 'samtools view -@ {THREADS} -bh {input} > {output}'

rule bam:
    input: rules.reads_unsorted.output
    output: 'reads.bam'
    benchmark: './benchmarks/bam.txt'
    shell: 'samtools sort -@ {THREADS} {input} -o {output}'

rule bai:
    input: rules.bam.output
    output: 'reads.bam.bai'
    benchmark: './benchmarks/bai.txt'
    shell: 'samtools index {input} {output}'

rule decompress_reference:
    input: rules.get_reference.output
    output: 'reference.genomic.fna'
    benchmark: './benchmarks/decompress_reference.txt'
    shell: 'bgzip -d {input} --stdout > {output}'

rule call:
    input:
        bam=rules.bam.output,
        alignment_index=rules.bai.output,
        reference=rules.decompress_reference.output
    output: 'reads.vcf'
    benchmark: './benchmarks/call.txt'
    shell: 'samtools mpileup -uf {input.reference} {input.bam} | bcftools call -c - > {output}'

# Cleanup

rule clean:
    shell: '''
        rm -rf {READS} || true;
        rm tmp || true;
        rm *.log || true;
        rm *.sra || true;
        rm *.fastq.gz || true;
        rm *.sam || true;
        rm *.bam.* *.bam || true;
        rm *.fna.* || true;
        rm *.fna || true;
        rm *.vcf || true;
    '''
